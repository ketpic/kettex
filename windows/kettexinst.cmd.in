@echo off

::: initial variables
set KETTEXROOT=C:\kettex
set KETTEXLOG=%KETTEXROOT%\install-kettex.log
set TEMPKETTEXINST=%TEMP%\kettexinst
set PATH=%TEMPKETTEXINST%\texinstwin;%SystemRoot%\system32;%SystemRoot%;

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
echo @@KETTEXPKG@@ installer

::: check for version later than vista
for /f "usebackq tokens=2 delims=[]" %%I in (`ver`) do set ver_str=%%I
set ver_str=%ver_str:* =%
::: windows 9x, 2000, xp, vista unsupported
if %ver_str:~,2% == 4. goto :TOOOLD
if %ver_str:~,2% == 5. goto :TOOOLD
if %ver_str:~,3% == 6.0 (
   echo WARNING: Windows 7 is the earliest supported version.
   echo TeX Live 2020 has not been tested on Windows Vista.
)
if "x86"=="%PROCESSOR_ARCHITECTURE%" (
if ""=="%PROCESSOR_ARCHITEW6432%" (
   echo 32-bit no longer supported
   goto :EOFF
))

::: check %KETTEXROOT%
if "%1" equ "" (
   echo.
) else (
  set KETTEXROOT=%1
)
echo checking KeTTeX root directory %KETTEXROOT% ...
if exist %KETTEXROOT%   goto :KETTEXEXISTS

echo done  checking KeTTeX root directory %KETTEXROOT%


::: prepare installation under %TEMP%
echo preparing KeTTeX installation in %TEMPKETTEXINST% ...
rmdir /S /Q %TEMPKETTEXINST%  >nul 2>&1
mkdir %TEMPKETTEXINST%
cd %~dp0
move @@KETTEXPKG@@.tar.zst %TEMPKETTEXINST%  >nul 2>&1
move texinstwin %TEMPKETTEXINST%  >nul 2>&1

echo done  preparing KeTTeX installation in %TEMPKETTEXINST%


::: start installation
echo installing KeTTeX in %KETTEXROOT% ... (It will take a while.)
mkdir %KETTEXROOT%
cd %KETTEXROOT%  >%KETTEXLOG% 2>&1
cd ..  >%KETTEXLOG% 2>&1
tar -xf %TEMPKETTEXINST%\@@KETTEXPKG@@.tar.zst  --totals
::: unsupported options: --checkpoint=1000 --checkpoint-action=dot

echo done  installing KeTTeX in %KETTEXROOT%


::: post process
set PATH=%KETTEXROOT%\texlive\bin\windows;%SystemRoot%\system32;%SystemRoot%;
echo running post process ...
::: tlmgr platform set windows
echo (1/3) running updmap-sys ...
updmap-sys                        >%KETTEXLOG% 2>&1
echo done  running updmap-sys
echo (2/3) running fmtutil-sys ... (It will take a while.)
fmtutil-sys --all                 >%KETTEXLOG% 2>&1
echo done  running fmtutil-sys
echo (3/3) running luaotfload-tool --update ... (It will take a while.)
luaotfload-tool --update --force  >%KETTEXLOG% 2>&1
echo done  running luaotfload-tool --update

echo done  running post process


echo Welcome to KeTTeX!
goto :END

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:TOOOLD
echo TeX Live does not run on this Windows version.
echo TeX Live is supported on Windows 7 and later.
goto :EOFF

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:KETTEXEXISTS
echo %KETTEXROOT% already exists!
echo kettexinst.cmd do not install @@KETTEXPKG@@ in %KETTEXROOT%
goto :EOFF

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:END
::: cleanup %TEMPKETTEXINST%
rmdir /S /Q %TEMPKETTEXINST%  >nul 2>&1
goto :EOFF

:EOFF
pause
